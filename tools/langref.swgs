/*
    This script is used to generate the documentation you can see at swag-lang.org.

    It takes the content of the swag-lang/swag/bin/reference/tests folders, and generates the
    corresponding documentations.
*/

#dependencies
{
    #import "gui" location="swag@std"
}

using Core, Gui

enum State
{
    Paragraph
    Code
    Test
    Blockquote
}

var g_Buf:  String  // Will contain the html page
var g_Toc:  String  // Will contain the left part, i.e. the table of content
var g_Code: String

var g_State:            Array'State
var g_CptBrace:         s32
var g_SubTitle:         bool

func stateEnter(st: State)
{
    switch st
    {
    case Paragraph:
        g_Buf += "<p>"

    case Blockquote:
        g_Buf += "<p class=\"blockquote\">"
    }
}

func stateLeave(st: State)
{
    switch st
    {
    case Paragraph:
        g_Buf += "</p>\n"
    case Blockquote:
        g_Buf += "</p>\n"

    case Code, Test:
        flushCode()
    }
}

func pushState(st: State)
{
    stateLeave(g_State.back())
    stateEnter(st)
    g_State.add(st)
}

func popState()
{
    stateLeave(g_State.back())
    g_State.removeBack()
    stateEnter(g_State.back())
}

// Add one line of 'text'
func addLine(line: string)
{
    var lineTrim: String = line
    lineTrim.trim()

    // Sub title if text has the form '# text'
    if Utf8.startsWith(lineTrim, "# ")
    {
        lineTrim.remove(0, 2)
        g_Buf += Format.toString("<h% id=\"%\">", g_SubTitle ? 4 : 3, lineTrim)
        addLine(lineTrim)
        g_Buf += g_SubTitle ? "</h4>" : "</h3>"

        g_Toc += Format.toString("<li><a href=\"#%\">%</a></li>\n", lineTrim, lineTrim);
        return
    }

    stateBold   := false
    stateItalic := false
    stateCode   := false

    // Process character by character, in order to detect some 'markdown' stuff
    for i := 0; i < @countof(line); i += 1
    {
        // Bold with '**'
        if line[i] == "*"'u8 and line[i+1] == "*"'u8
        {
            g_Buf += stateBold ? "</b>" : "<b>"
            stateBold = !stateBold
            i += 1
            continue
        }

        // Italic with '*'
        if line[i] == "*"'u8
        {
            if !stateItalic and Latin1.isLetterOrDigit(line[i+1])
            {
                stateItalic = true
                g_Buf += "<i>"
                continue
            }

            if stateItalic
            {
                g_Buf += "</i>"
                stateItalic = false
                continue
            }
        }

        // Inlined code with '`'
        if line[i] == "`"'u8
        {
            if !stateCode
            {
                stateCode = true
                g_Buf += "<code>"
                continue
            }

            if stateCode
            {
                g_Buf += "</code>"
                stateCode = false
                continue
            }
        }

        // Reference
        if line[i] == "["'u8
        {
            var linkName: String
            i += 1
            while line[i] and line[i] != "]"'u8
            {
                linkName += line[i]
                i += 1
            }

            if line[i]
                i += 1

            if line[i] != "("'u8
            {
                g_Buf += "["
                g_Buf += linkName
                g_Buf += "]"
                continue
            }

            i += 1
            var linkValue: String
            while line[i] and line[i] != ")"'u8
            {
                linkValue += line[i]
                i += 1
            }

            g_Buf += Format.toString("<a href=\"%\">%</a>", linkValue, linkName)
            continue
        }

        g_Buf += line[i]
    }

    g_Buf += "</br>\n"
}

func flushCode()
{
    // Remove empty lines before and after the code block
    while g_Code.length and g_Code[0] == "\n"'u8
        g_Code.remove(0, 1)
    while g_Code.length and g_Code.back() == "\n"'u8
        g_Code.removeBack()
    if g_Code.length == 0
        return

    g_Buf += "<pre>"
    g_Buf += "<code>"

    var g_Edit:      RichEditCtrl
    var g_LexerSwag: RichEditLexerSwag

    g_Edit.setLexer(cast(IRichEditLexer) g_LexerSwag)
    g_Edit.setText(g_Code)
    g_Edit.lexAll()

    res := g_Edit.getTextAndStyles()

    g_Edit.styles[0].colFg = Pixel.Argb.Black
    lastStyle := -1
    loop i: res.text
    {
        if res.styles[i] != lastStyle
        {
            if lastStyle != -1
                g_Buf += "</span>"
            lastStyle = res.styles[i]

            color := g_Edit.styles[lastStyle].colFg
            hsl := color.toHsl()
            hsl.l = Math.clamp(hsl.l, 0, 0.5)
            color = Pixel.Color.fromHsl(hsl.h, hsl.s, hsl.l)

            g_Buf += Format.toString("<span style=\"color:#%{X}\">", color.argb & 0xFFFFFF)
        }

        g_Buf += res.text[i]
    }

    g_Buf += "</span>"
    g_Buf += "</code>"
    g_Buf += "</pre>"

    g_Code.clear()
}

// Add one line of code
func addLineCode(l: string)
{
    g_Code += l
    g_Code += "\n"
}

func processFile(fileName: string)
{
    lines := assume File.readAllLines(fileName)

    stateEnter(.Code)
    g_State.add(State.Code)

    visit l: lines
    {
        lineTrim := l
        lineTrim.trim()

        curState := g_State.back()

        // Some state changes
        if Utf8.startsWith(lineTrim, "/**")
        {
            pushState(.Paragraph)
            continue
        }

        if curState == .Paragraph and Utf8.startsWith(lineTrim, ">")
        {
            pushState(.Blockquote)
            curState = g_State.back()
        }

        if curState == .Blockquote and !Utf8.startsWith(lineTrim, ">")
        {
            popState()
            curState = g_State.back()
        }

        switch curState
        {
        //////////////////////////////////
        case Blockquote:
            lineTrim.remove(0, 1)
            lineTrim.trim()
            addLine(lineTrim)

        //////////////////////////////////
        case Paragraph:
            if Utf8.startsWith(lineTrim, "```")
                pushState(.Code)
            elif Utf8.startsWith(lineTrim, "*/")
                popState()
            else
                addLine(lineTrim)

        //////////////////////////////////
        case Test:
            if lineTrim == "{"
            {
                g_CptBrace += 1
                if g_CptBrace == 1
                    continue
            }
            elif lineTrim == "}"
            {
                g_CptBrace -= 1
                if g_CptBrace == 0
                {
                    popState()
                    continue
                }
            }

            var nextLine: String
            if @index != lines.count - 1
            {
                nextLine = lines[@index+1]
                nextLine.trim()
            }

            if g_CptBrace == 1 and Utf8.startsWith(lineTrim, "//") and nextLine == "{"
            {
                lineTrim.remove(0, 2)
                popState()
                pushState(.Paragraph)
                addLine("\n")
                addLine(lineTrim)
                popState()
                pushState(.Test)
                continue
            }

            lineLine := l
            if Utf8.startsWith(lineLine, "    ")
                lineLine.remove(0, 4)
            addLineCode(lineLine)

        //////////////////////////////////
        case Code:
            if Utf8.startsWith(l, "```")
                popState()
            elif Utf8.startsWith(l, "#test")
                pushState(.Test)
            else
                addLineCode(l)
        }
    }

    // Unstack all states
    while g_State.count
        stateLeave(g_State.popBack())
}

func generate(in, out: string)
{
    g_Buf.clear()
    g_Toc.clear()
    g_Code.clear()
    g_State.clear()
    g_CptBrace = 0
    g_SubTitle = false

    files := assume Directory.enumerateFiles(in, ".swg", recurse: true)
    files.sort(func(a, b) => a.fullname <=> b.fullname)

    g_Buf += "<?php include('html_start.php'); include('header.php'); ?>\n"
    //g_Buf += "<html lang=\"en\">\n"
    //g_Buf += "<head>\n"
    //g_Buf += "<meta charset=\"UTF-8\">\n";
    //g_Buf += "</head>\n"

    // Create styles
    g_Buf += """
        <style>
        h1, h2, h3, h4, h5, h6 {
            margin:         revert;
            font-size:      revert;
            font-weight:    revert;
        }

        p code {
            background-color:   #eeeeee;
            border:             1px dotted #cccccc;
            padding:            2px;
            margin:             0px;
            font-size:          0.8em;
        }

        pre {
            background-color:   #eeeeee;
            border:             1px solid LightGrey;
            margin:             20px 0;
            padding:            20px;
            font-size:          0.8em;
            white-space:        break-spaces;
            overflow-wrap:      break-word;
        }

        p a {
            text-decoration: none;
            color:           Blue;
        }

        p a:hover {
            text-decoration: underline;
        }

        .blockquote {
            border-left:    2px solid Orange;
            margin-left:    10px;
            padding-left:   10px;
        }

        .container {
            height: 100%;
            font-family: Segoe UI;
            line-height: 1.3em;
        }

        .left {
            overflow-y: scroll;
        }

        .right {
            overflow-y: scroll;
            padding-left: 10px;
        }

        .container ul, .tocbullet {
            list-style-type:    revert;
            padding:            revert;
        }

        .container a {
            text-decoration: none;
        }

        .container a:hover {
            text-decoration: underline;
        }

        @media (min-width: 1024px) {
            html, body {
                height: 100%;
            }

            pre {
                margin: 20px;
            }
        }
        </style>
    """

    // Main page (left and right parts, left is for table of content, right is for content)
    g_Buf += "<div class=\"lg:m-auto lg:w-[76rem] pt-5 lg:pt-10 overflow-hidden p-5\" style=\"height: 93%;\">\n"
    g_Buf += "<div class=\"container lg:flex\">\n"
    g_Buf += "<div class=\"left lg:w-[450px] lg:h-full x-data=\"{ open_content: false }\"\">\n"
    seekToc := g_Buf.length // This will be the place where we will insert the 'g_Toc' table of content
    g_Buf += "</div>\n"

    // Right page start
    g_Buf += "<div class=\"right lg:w-full lg:h-full lg:pl-2\">\n"
    g_Buf += "<div class=\"page\">\n"
    g_Buf += "<h1 class=\"leading-8 mt-10 lg:my-5\">Swag language reference</h1>\n"

    // Table of content start
    g_Toc += "<div class=\"flex items-center justify-between border lg:border-0 rounded pt-1 px-2\" @click=\"open_content = ! open_content\">\n"
    g_Toc += "<h1 class=\"leading-8 m-0 lg:my-5\">Content</h1>\n"
    g_Toc += "<div class=\"leading-8 block lg:hidden\"><img src=\"imgs/chevron.png\" alt=\"\" class=\"w-5\" :class=\"open_content ? 'rotate-90' : ''\" /></div>\n"
    g_Toc += "</div>\n"
    g_Toc += "<ul class=\"mt-5\" :class=\"open_content ? '' : 'hidden lg:block'\">\n"

    visit v: files
    {
        if Path.getExtensionLowerCase(v.fullname) != ".swg"
            continue

        var title: String = Path.getFileNameWithoutExtension(v.fullname)
        fileName := title
        title = title[4..]

        // If there's an additional '_' after 'NNN_', then this is a 'sub part'.
        wasSubTitle := g_SubTitle
        g_SubTitle = title[0] == "_"'u8
        if g_SubTitle
            title = title[1..]
        if !wasSubTitle and g_SubTitle
            g_Toc += "<ul class=\"tocbullet\">\n"
        else if wasSubTitle and !g_SubTitle
            g_Toc += "</ul>\n"

        // Normalize filename as a title
        title[0] = Latin1.toUpper(title[0])
        title.replace("_", " ")

        // Add title to output
        g_Buf += "\n"
        g_Buf += Format.toString("<h% id=\"%\">", g_SubTitle ? 3 : 2, fileName)
        g_Buf += title
        g_Buf += g_SubTitle ? "</h3>\n" : "</h2>\n"

        // Add title to table of content
        g_Toc += Format.toString("<li><a href=\"#%\">%</a></li>\n", fileName, title);
        g_Toc += "<ul class=\"tocbullet\">\n"

        processFile(v.fullname)

        g_Toc += "</ul>\n"
    }

    g_Toc += "</ul>\n"

    // Close html page
    g_Buf +=
        """
        </div>
        </div>
        </div>
        """

    //g_Buf += "</html>\n"
    g_Buf += "<?php include('html_end.php'); ?>\n"

    g_Buf.insert(seekToc, g_Toc)

    assume File.writeAllBytes(out, g_Buf)
}

#run
{
    generate("../bin/reference/tests/language/src", "../web/language.php")
}