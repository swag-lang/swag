// https://youtu.be/ZI1dmHv3MeM?list=PLRqwX-V7Uu6ZiZxtDDRCi6uhfTH4FilpH

#dependencies
{
    #import "gui" location="swag@std"
}

using Core, Gui, Pixel, Math
const NoiseMax = 5.0

func onEvent(wnd: *Wnd, evt: *Event)->bool
{
    if evt.kind != .Paint
        return false

    paintEvt := cast(*PaintEvent) evt
    painter  := paintEvt.bc.painter

    centerX := wnd.position.width * 0.5
    centerY := wnd.position.height * 0.5

    #[Swag.Global]
    var zoff: f32
    zoff += wnd.getApp().getDt()

    var lp: LinePath
    for a := 0'f32; a < ConstF32.TwoPi; a += 0.1
    {
        xoff := map(cos(a), -1, 1, 0, NoiseMax)
        yoff := map(sin(a), -1, 1, 0, NoiseMax)
        r    := map(Noise.perlin(xoff, yoff, zoff), -1, 1, 100, 200)

        x := r * cos(a)
        y := r * sin(a)
        if a == 0
            lp.start(x, y)
        else
            lp.curveTo(x, y)
    }

    lp.close()

    painter.setParams(.Antialiased)
    painter.clear(Argb.Black)
    painter.translateTransform(centerX, centerY)
    painter.fillPath(&lp, Argb.DarkBlue)
    painter.drawPath(&lp, Argb.White, 2)

    wnd.invalidate()
    return true
}

#run
{
    Application.runSurface(100, 100, 512, 512, title: "Blob", hook: &onEvent)
}