// https://youtu.be/4hA7G3gup-4

#dependencies
{
    #import "gui" location="swag@std"
}

using Core, Gui, Pixel, Math

var g_Edit: *EditBox
var g_ComputeValue: u64
var g_CurValue: u64

#[Swag.Mixin]
func createBtn(str: string, x, y: s32)
{
    {
        btn := PushButton.create(layout, str)
        btn.sigPressed += func(btn) {
            switch btn.name
            {
            case "-": break
            case "+": break
            case "*": break
            case "=": break
            default:
                v := assume StrConv.toS64(btn.name.raw)
                g_CurValue *= 10
                g_CurValue += v
                break
            }
            g_Edit.setText(Format.toString("%", g_CurValue))
        }

        layout.setColRowChild(btn, x, y)

        switch str
        {
        case "=":           btn.style.addStyleSheetColors("btnPush_Bk 0xFF880000")
        case "+", "-", "*": btn.style.addStyleSheetColors("btnPush_Bk 0xFF008800")
        }
    }
}

func onEvent(wnd: *Wnd, evt: *Event)->bool
{
    switch evt.kind
    {
    case Create:
        with layout := GridLayoutCtrl.create(wnd, 4, 5)
        {
            .dockStyle = .Center
            .gridLayoutFlags = .AdaptColWidth | .AdaptRowHeight
            .backgroundStyle = .Window
            .margin = 10

            g_Edit = EditBox.create(layout, "0")
            .setColRowChild(g_Edit, 0, 0, 4)

            createBtn("7", 0, 1)
            createBtn("8", 1, 1)
            createBtn("9", 2, 1)
            createBtn("4", 0, 2)
            createBtn("5", 1, 2)
            createBtn("6", 2, 2)
            createBtn("1", 0, 3)
            createBtn("2", 1, 3)
            createBtn("3", 2, 3)
            createBtn("0", 1, 4)

            createBtn("*", 3, 1)
            createBtn("-", 3, 2)
            createBtn("+", 3, 3)
            createBtn("=", 3, 4)

            .computeLayout()
        }
    }

    return false
}

#run
{
    func test(app: *Application) = if Env.hasArg("swag.test") app.maxRunFrame = 100
    Application.runSurface(100, 100, 400, 400, title: "Calc", hook: &onEvent, init: &test)
}