#dependencies
{
    #import "gui" location="swag@std"
}

using Core, Gui, Pixel
const GridSize = 24
const CellSize = 30'f32
const FillRate = 10

struct Cell
{
    mine:   bool
    show:   bool
    score:  s32
}

var g_Grid:     [GridSize, GridSize] Cell
var g_FontCell: *Font

func fillGrid()
{
    var rng: Random.DefaultRng
    rng.seedNow()

    // Put a mine at random
    visit &g: g_Grid
    {
        if rng.nextU32(0, 100) < FillRate
            g[0].mine = true
    }

    // Count neighbours for each cell
    loop i: GridSize
    {
        loop j: GridSize
        {
            loop ii: -1..1
            {
                loop jj: -1..1
                {
                    ti := i + ii
                    tj := j + jj
                    if ti < 0 or ti >= GridSize or tj < 0 or tj >= GridSize
                        continue
                    if g_Grid[ti, tj].mine
                        g_Grid[i, j].score += 1
                }
            }
        }
    }
}

func onEvent(wnd: *Wnd, evt: *Event)->bool
{
    switch evt.kind
    {
    case Create:
        fillGrid()
        g_FontCell = wnd.getTheme().createDefaultFont(20)

    case MousePressed:
        mouseEvt := cast(*MouseEvent) evt
        pos := wnd.surfaceToLocal(mouseEvt.surfacePos)
        x := cast(s32) (pos.x / CellSize)
        y := cast(s32) (pos.y / CellSize)
        x, y -= 1
        if x < 0 or x >= GridSize or y < 0 or y >= GridSize
            break
        g_Grid[x, y].show = true
        wnd.invalidate()
        return true

    case Paint:
        paintEvt := cast(*PaintEvent) evt
        painter  := paintEvt.bc.painter
        painter.clear(Argb.White)

        // Paint grid
        loop i: GridSize
        {
            x := CellSize + (i * CellSize)
            w := CellSize * GridSize
            painter.drawRect(CellSize, x, w, CellSize, Argb.Black)
            painter.drawRect(x, CellSize, CellSize, w, Argb.Black)
        }

        // Paint content
        painter.setParams(.Antialiased)
        font := wnd.getTheme().res.fontDefault.regular
        loop i: GridSize
        {
            loop j: GridSize
            {
                x    := CellSize + (i * CellSize)
                y    := CellSize + (j * CellSize)
                rect := Math.Rectangle{x, y, CellSize, CellSize}

                if !g_Grid[i, j].show
                {
                    rect.inflate(-2)
                    painter.fillRect(rect, Argb.DarkGray)
                }
                elif g_Grid[i, j].mine
                {
                    painter.fillCircle(x + CellSize * 0.5, y + CellSize * 0.5, CellSize * 0.4, Argb.Red)
                }
                elif g_Grid[i, j].score
                {
                    str  := Format.toString("%", g_Grid[i, j].score)

                    var rs:  RichString
                    var rsf: RichStringFormat
                    rs.set(str)
                    rsf.font.regular = g_FontCell
                    rsf.horzAlignment = .Center
                    rsf.vertAlignment = .Center
                    rsf.palette[0] = Argb.Black
                    painter.drawRichString(rect, &rs, rsf)
                }
            }
        }
    }

    return false
}

#run
{
    var app: Application
    try app.runSurface(100, 100, 1024, 1024, title: "Minesweeper", hook: &onEvent)
}
