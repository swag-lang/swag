using Core

#dependencies
{
    #import("core", location: "swag@std")
}

func filter(file: File.FileInfo)->bool
{
    if Utf8.contains(file.fullname, "/vendor/") do
        return false
    if Utf8.contains(file.fullname, "/output/") do
        return false
    if Utf8.contains(file.fullname, "/dependencies/") do
        return false
    if Utf8.contains(file.fullname, "/public/") do
        return false
    if Utf8.contains(file.fullname, "/datas/") do
        return true
    if Utf8.contains(file.fullname, "/paint/theme/") do
        return true
    if Utf8.contains(file.fullname, "/shaders/") do
        return true

    var ext = Path.getExtensionLowerCase(file.fullname)
    switch ext
    {
    case ".exe":
        return Utf8.contains(file.fullname, "swag.exe") or
               Utf8.contains(file.fullname, "swag_stats.exe")
    case ".dll", ".lib", ".swg", ".glsl", ".swgs":
        return true
    }

    return false
}

#main
{
    let toolsPath = "c:/perso/swag-lang/swag/tools"
    let dataPath  = Path.getDirectoryName(toolsPath)
    var binPath   = Path.combine(dataPath, "bin")

    // Enumerate list of files to release
    with var options: Directory.EnumerationOptions
    {
        .wantDirectories = false
        .recurse         = true
        .filterLambda    = &filter
    }

    var allFiles = try Directory.enumerate(binPath, options)

    // Generate list of files
    var allLines: Array'string
    foreach f in allFiles do
        allLines.add(cast(string) f.fullname[9 to ])

    let mo = try Directory.getCurrent()
    try Directory.setCurrent(Path.getDirectoryName(Path.getDirectoryName(dataPath)))

    catch File.delete("list.txt")
    try File.writeAllLines("list.txt", allLines)

    // Zip them
    var name = Format.toString("swag-windows-x86-64-v%.%.%.zip", #swagversion, #swagrevision, #swagbuildnum)
    var cmd  = Format.toString("a % @list.txt", name)
    Console.print("7z.exe ", cmd, "\n")
    try Env.doSyncProcess(Path.combine(toolsPath, "7z.exe"), cmd)

    catch File.delete("list.txt")
    try Directory.setCurrent(mo)
}
