#global public
using Core

struct ThemeStyleRef
{
    theme:  *Theme
    count:  u32
}

struct ThemeStyle
{
    refr:               *ThemeStyleRef
    dirty:              bool
    styleSheetMetrics:  String
    styleSheetColors:   String
}

impl ThemeStyle
{
    mtd opDrop()
    {
        free()
    }

    private mtd free()
    {
        if !refr
            return
        refr.count -= 1
        if refr.count == 0
        {
            Memory.delete(refr.theme)
            Memory.delete(refr)
            refr = null
        }
    }

    private mtd set(theme: *Theme)
    {
        refr = Memory.new'ThemeStyleRef()
        refr.theme = theme
        refr.count = 1
    }


    // Create a specific style based on the parent
    mtd create(parent: *ThemeStyle)
    {
        free()
        theme := Memory.new'Theme()
        dref theme = dref parent.refr.theme
        set(theme)
    }

    private mtd compute(owner: *Wnd, parent: *ThemeStyle)
    {
        // First time, initialize to parent
        if !refr
        {
            refr = parent.refr
            refr.count += 1
            dirty = true
        }

        // If parent is dirty, or i am dirty, then we need
        // to compute.
        if !dirty and !parent.dirty
            return
        dirty = true

        // By default, same as the parent
        free()
        refr = parent.refr
        refr.count += 1

        // Ask user
        evt := SetThemeEvent{}
        evt.accepted = false
        evt.style = self
        evt.parent = parent
        owner.sendEvent(&evt)

        // Nothing to override
        if styleSheetColors.length == 0 and styleSheetMetrics.length == 0 and !evt.accepted
            return

        // Make a new tweak as a copy of the parent
        if refr == parent.refr
            create(parent)

        // Then tweak it with the stylesheet
        if styleSheetColors.length
        {
            var tf: File.TweakFile
            tf.registerFolder'ThemeColors(&refr.theme.colors)
            catch tf.parse(styleSheetColors)
        }

        if styleSheetMetrics.length
        {
            var tf: File.TweakFile
            tf.registerFolder'ThemeMetrics(&refr.theme.metrics)
            catch tf.parse(styleSheetMetrics)
        }
    }

    mtd clearStyleSheetColors()
    {
        styleSheetColors.clear()
        dirty = true
    }

    mtd setStyleSheetColors(str: string)
    {
        styleSheetColors = str
        dirty = true
    }

    mtd addStyleSheetColors(str: string)
    {
        styleSheetColors.append("\n")
        styleSheetColors.append(str)
        dirty = true
    }

    mtd setStyleSheetMetrics(str: string)
    {
        styleSheetMetrics = str
        dirty = true
    }

    mtd addStyleSheetMetrics(str: string)
    {
        styleSheetMetrics.append("\n")
        styleSheetMetrics.append(str)
        dirty = true
    }
}