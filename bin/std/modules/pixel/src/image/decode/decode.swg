using Swag, Core

struct ImageDecoder
{
    type:   const *TypeInfoStruct
    itf:    IImageDecoder
}

public struct DecodeOptions
{
    // Decode image informations (width, height...) but do no read pixels
    // Used to fast retrieve informations about the image
    decodePixels = true
}

// Interface to decode a buffer
public interface IImageDecoder
{
    canDecode:  func(self, string)->bool
    decode:     func(self, const [..] u8, DecodeOptions)->Image throw
}

impl Image
{
    var decoders: Array'ImageDecoder

    // Find the decoder that can treat 'fileName'
    func getDecoder(fileName: string)->*ImageDecoder
    {
        var found: *ImageDecoder
        visit &decoder: decoders
        {
            if decoder.itf.canDecode(fileName)
            {
                found = decoder
                break
            }
        }

        return found
    }

    // Decode the given image buffer with the given decoder
    func decode(decoder: *ImageDecoder, bytes: const [..] u8, options: DecodeOptions = @{})->Image throw
    {
        // Create a decoder instance
        ptrDecoder := Memory.new(decoder.type)
        defer
        {
            if decoder.type.opDrop
                decoder.type.opDrop(ptrDecoder)
            Memory.free(ptrDecoder, decoder.type.sizeof)
        }

        // Decode
        itf := @mkinterface(ptrDecoder, decoder.type, IImageDecoder)
        return try itf.decode(bytes, options)
    }
}


// Load an image from file
public impl Image
{
    // Register an image decoder
    func(T) addDecoder()
    {
        #assert Reflection.hasInterface(@typeof(T), IImageDecoder)

        var decoder: ImageDecoder = ?
        decoder.type = @typeof(T)
        decoder.itf = @mkinterface(null, @typeof(T), IImageDecoder)
        decoders.add(decoder)
    }

    // Returns 'true' if the given filename has a corresponding decoder
    func canLoad(fileName: string)->bool
    {
        visit &decoder: decoders
        {
            if decoder.itf.canDecode(fileName)
                return true
        }

        return false
    }

    // Decode the given image buffer
    func decode(fileName: string, bytes: const [..] u8, options: DecodeOptions = @{})->Image throw
    {
        found := getDecoder(fileName)
        if !found
            throw "no matching image decoder"
        return decode(found, bytes, options)
    }

    // Load the given image file
    func load(fileName: string, options: DecodeOptions = @{})->Image throw
    {
        found := getDecoder(fileName)
        if !found
            throw "no matching image decoder"
        bytes := File.readAllBytes(fileName)
        return decode(found, bytes.toSlice(), options)
    }
}

// Initialize predefined decoders
#init
{
    Image.addDecoder'(Bmp.Decoder)()
    Image.addDecoder'(Tga.Decoder)()
    Image.addDecoder'(Jpg.Decoder)()
    Image.addDecoder'(Png.Decoder)()
    Image.addDecoder'(Gif.Decoder)()
}